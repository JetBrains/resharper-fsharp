options {
  parserPackage="JetBrains.ReSharper.Plugins.FSharp.Psi.Gen";
  parserClassName="FSharpParserGenerated";
  psiInterfacePackageName="JetBrains.ReSharper.Plugins.FSharp.Psi.Tree";
  psiStubsPackageName="JetBrains.ReSharper.Plugins.FSharp.Psi.Impl.Tree";
  psiStubsBaseClass="JetBrains.ReSharper.Plugins.FSharp.Psi.Impl.Tree.FSharpCompositeElement";
  tokenClassName="JetBrains.ReSharper.Plugins.FSharp.Psi.Impl.Tree.FSharpToken";
  tokenTypePackageName="JetBrains.ReSharper.Plugins.FSharp.Psi.Parsing";
  tokenTypeClassName="FSharpTokenType";
  parserTargetSubdir="Parsing/FSharp";
  psiInterfacesTargetSubdir="Psi/FSharp";
  psiStubsTargetSubdir="Psi/FSharp/Impl";
  elementTypePrefix="";
  visitorClassName="TreeNodeVisitor";
  visitorMethodSuffix="";
  disableReflection;
  separateHierarchies;
  acceptModifier="";
  visitorSuperClassName="JetBrains.ReSharper.Plugins.FSharp.Psi.TreeNodeVisitorBase";
  "treeElementClassFQName"="JetBrains.ReSharper.Psi.ExtensionsAPI.Tree.TreeElement";
  "leafElementClassFQName"="JetBrains.ReSharper.Plugins.FSharp.Psi.Impl.Tree.FSharpLeafElement";
  "compositeElementClassFQName"="JetBrains.ReSharper.Plugins.FSharp.Psi.Impl.Tree.FSharpCompositeElement";
  "psiElementVisitorClassFQName"="JetBrains.ReSharper.Plugins.FSharp.Psi.Tree.TreeNodeVisitor";
  "unexpectedTokenClassFQName"="Parsing.UnexpectedToken";
  "syntaxErrorClassFQName"="Parsing.SyntaxError";
  "parserGenRuntimePackageName"="Parsing";
  "createTokenElementMethodFQName"="JetBrains.ReSharper.Plugins.FSharp.Psi.Impl.Factory.createTokenElement";
  "visitMethodPrefix"="Visit";
  "lexerClassName"="JetBrains.ReSharper.Plugins.FSharp.Psi.Parsing.FSharpLexer";
  "psiClassesPrefix"="";
  "psiElementClassFQName"="JetBrains.ReSharper.Plugins.FSharp.Psi.Tree.IFSharpTreeNode";
  customImplPackage="JetBrains.ReSharper.Plugins.FSharp.Psi.Impl.Tree";
  customInterfacePackage="JetBrains.ReSharper.Plugins.FSharp.Psi.Tree";
  "interfaceNamePrefix"="I";
  "tokenElementClassFQName"="JetBrains.ReSharper.Psi.Tree.ITokenNode";
  "customImplSuffix"="";
  "objectClassFQName"="System.Object";
  publicChildRolePrefix="FS_";
  tokenBitsetThreshold=4;
  elementTypeBaseClass="JetBrains.ReSharper.Plugins.FSharp.Psi.Tree.FSharpCompositeNodeType";
  parserMessagesClass="JetBrains.ReSharper.Plugins.FSharp.Psi.Parsing.ParserMessages";
  generateWorkingPsi;
}



// Files

interface fSharpFile options { customParseFunction; noInternalParseFunction; }:
  fSharpImplFile |
  fSharpSigFile;

fSharpImplFile options { stubBase="FSharpFileBase"; }:
  topLevelModuleLikeDeclaration<MODULE_OR_NAMESPACE_DECLARATION, Declarations>*;

fSharpSigFile options { stubBase="FSharpFileBase"; }:
  topLevelModuleLikeDeclaration<MODULE_OR_NAMESPACE_DECLARATION, Declarations>*;



// Modules and namespaces

interface fSharpDeclaration:
  moduleLikeDeclaration |
  moduleMemberDeclaration |
  fSharpTypeDeclaration;

interface moduleLikeDeclaration:
  topLevelModuleLikeDeclaration |
  nestedModuleDeclaration;

interface topLevelModuleLikeDeclaration:
  namedModuleLikeDeclaration |
  anonModuleDeclaration |
  globalNamespaceDeclaration;

interface namedModuleLikeDeclaration:
  namedModuleDeclaration |
  namedNamespaceDeclaration;

interface moduleDeclaration:
  namedModuleDeclaration |
  nestedModuleDeclaration;


namedNamespaceDeclaration options { stubBase="FSharpDeclarationBase"; }:
  NAMESPACE<NAMESPACE, NamespaceToken>
  longIdentifier<LONG_IDENTIFIER, LongIdentifier>
  moduleMember<MODULE_MEMBER, Members>*;

globalNamespaceDeclaration options { stubBase="FSharpDeclarationBase"; }:
  NAMESPACE<NAMESPACE, NamespaceToken>
  GLOBAL
  moduleMember<MODULE_MEMBER, Members>*;

namedModuleDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; }:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  MODULE<MODULE, ModuleToken>
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  longIdentifier<LONG_IDENTIFIER, LongIdentifier>
  moduleMember<MODULE_MEMBER, Members>*;

anonModuleDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; }:
  moduleMember<MODULE_MEMBER, Members>*;



// Module members

interface moduleMember:
  moduleMemberDeclaration |
  moduleMemberStatement |
  memberDeclaration |
  hashDirective |
  otherModuleMember;

interface moduleMemberDeclaration:
  nestedModuleDeclaration |
  fSharpTypeDeclaration |
  letModuleDecl |
  moduleAbbreviation |
  otherMemberDeclaration;

interface moduleMemberStatement:
  openStatement |
  do;

letModuleDecl options { stubBase="DummyDeclaration"; }:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  letOrUse{LET, LetOrUseToken}
  INLINE<INLINE, InlineKeyword>?
  REC<REC, RecKeyword>?
  binding<BINDING, Bindings>
  (
    AND<AND, Separators>
    binding<BINDING, Bindings>
  )*;


interface let:
  letBindings |
  letOrUseBangExpr;

interface letBindings:
  letModuleDecl |
  letOrUseExpr;

interface binding:
  topBinding |
  localBinding;

topBinding
  extras {
    get { methodName="Expression" path=<topBinding:CHAMELEON_EXPR/chameleonExpression:EXPR> };
  }:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  MUTABLE<MUTABLE, MutableKeyword>
  synPat<HEAD_PATTERN, HeadPattern>
  returnTypeInfo<RETURN_INFO, ReturnTypeInfo>
  EQUALS
  chameleonExpression<CHAMELEON_EXPR, ChameleonExpression>;

localBinding:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  MUTABLE<MUTABLE, MutableKeyword>
  synPat<HEAD_PATTERN, HeadPattern>
  returnTypeInfo<RETURN_INFO, ReturnTypeInfo>
  EQUALS
  synExpr<EXPR, Expression>;

returnTypeInfo:
  COLON
  fSharpAttribute<ATTRIBUTE, Attributes>*
  synType<TYPE, ReturnType>;

interface synPat:
  namedPat |
  typedPat |
  attribPat |
  orPat |
  andsPat |
  tuplePat |
  arrayOrListPat |
  parenPat |
  recordPat |
  optionalValPat |
  isInstPat |
  consPat |
  wildPat |
  constPat |
  otherPat;

interface namedPat:
  referencePat |
  parametersOwnerPat |
  asPat;

interface referencePat:
  localReferencePat |
  topReferencePat;

// todo: fix active pattern and operator decls
interface parametersOwnerPat:
  localParametersOwnerPat
  topParametersOwnerPat;

interface asPat:
  localAsPat |
  topAsPat;


localReferencePat options { stubBase="LocalPatternDeclarationBase"; }extras {
  get { methodName="Identifier" path=<localReferencePat:REFERENCE_NAME/expressionReferenceName:IDENTIFIER> };
}:
  expressionReferenceName<REFERENCE_NAME, ReferenceName>;

topReferencePat options { stubBase="TopPatternDeclarationBase"; } extras {
  get { methodName="Identifier" path=<topReferencePat:REFERENCE_NAME/expressionReferenceName:IDENTIFIER> };
}:
  expressionReferenceName<REFERENCE_NAME, ReferenceName>;

localParametersOwnerPat options { stubBase="LocalPatternDeclarationBase"; } extras {
  get { methodName="Identifier" path=<localParametersOwnerPat:REFERENCE_NAME/expressionReferenceName:IDENTIFIER> };
}:
  expressionReferenceName<REFERENCE_NAME, ReferenceName>
  synPat<PARAMETER, Parameters>*;

topParametersOwnerPat options { stubBase="TopPatternDeclarationBase"; } extras {
  get { methodName="Identifier" path=<topParametersOwnerPat:REFERENCE_NAME/expressionReferenceName:IDENTIFIER> };
}:
  expressionReferenceName<REFERENCE_NAME, ReferenceName>
  synPat<PARAMETER, Parameters>*;

localAsPat options { stubBase="LocalPatternDeclarationBase"; }:
  synPat<PATTERN, Pattern>
  AS
  fSharpIdentifier<IDENTIFIER, Identifier>;

topAsPat options { stubBase="TopPatternDeclarationBase"; }:
  synPat<PATTERN, Pattern>
  AS
  fSharpIdentifier<IDENTIFIER, Identifier>;


interface arrayOrListPat:
  arrayPat |
  listPat;

typedPat options { stubBase="SynPatternBase"; }:
  synPat<PATTERN, Pattern>
  COLON
  synType<TYPE, Type>;

attribPat options { stubBase="SynPatternBase"; }:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  synPat<PATTERN, Pattern>;

orPat options { stubBase="SynPatternBase"; }:
  synPat<PATTERN1, Pattern1>
  BAR
  synPat<PATTERN2, Pattern2>;

andsPat options { stubBase="SynPatternBase"; }:
  synPat<PATTERN1, Pattern1>
  AMP
  synPat<PATTERN2, Pattern2>;

arrayPat options { stubBase="SynPatternBase"; }:
  LBRACK_BAR
  synPat<ATTRIBUTE, Patterns>*
  LBRACK_BAR;

listPat options { stubBase="SynPatternBase"; }:
  LBRACK
  synPat<ATTRIBUTE, Patterns>*
  RBRACK;

tuplePat options { stubBase="SynPatternBase"; }:
  synPat<ATTRIBUTE, Patterns>
  (
    COMMA
    synPat<ATTRIBUTE, Patterns>
  )*;

parenPat options { stubBase="SynPatternBase"; }:
  LPAREN
  synPat<PATTERN, Pattern>
  RPAREN;

recordPat options { stubBase="SynPatternBase"; }:
  LBRACE
  recordFieldPat<FIELD_PAT, FieldPatterns>*
  RBRACE;

recordFieldPat:
  expressionReferenceName<REFERENCE_NAME, ReferenceName>
  EQUALS
  synPat<PAT, Pattern>;

optionalValPat options { stubBase="SynPatternBase"; }:
  QMARK
  synPat<PATTERN, Pattern>;

isInstPat options { stubBase="SynPatternBase"; }:
  COLON_QMARK
  synType<SYN_TYPE, Type>;

consPat options { stubBase="SynPatternBase"; }:
  synPat<PATTERN1, Pattern1>
  COLON_COLON
  synPat<PATTERN2, Pattern2>;

wildPat options { stubBase="SynPatternBase"; }:
  UNDERSCORE;

constPat options { stubBase="SynPatternBase"; }:
  (
    TRUE |
    FALSE |
    STRING // todo: add more
  );
  

otherPat options { stubBase="SynPatternBase"; }:
  fakeSeparator;


interface synExpr:
  parenExpr |
  quoteExpr |
  constExpr |
  typedExpr |
  tupleExpr |
  anonRecdExpr |
  arrayOrListExpr |
  recordExpr |
  newExpr |
  objExpr |
  whileExpr |
  forExpr |
  forEachExpr |
  arrayOrListOfSeqExpr |
  compExpr |
  lambdaExpr |
  matchLambdaExpr |
  matchExpr |
  doExpr |
  assertExpr |
  appExpr |
  letOrUseExpr |
  tryWithExpr |
  tryFinallyExpr |
  lazyExpr |
  sequentialExpr |
  ifThenElseExpr |
  referenceExpr |
  setExpr |
  indexerExpr |
  typeTestExpr |
  castExpr |
  inferredCastExpr |
  nullExpr |
  addressOfExpr |
  traitCallExpr |
  joinInExpr |
  implicitZeroExpr |
  yieldOrReturnExpr |
  letOrUseBangExpr |

  // todo: cover cases
  libraryOnlyExpr |
  fromErrorExpr |

  fixedExpr;

interface appExpr:
  prefixAppExpr |
  infixAppExpr;

setExpr options { stubBase="FSharpExpressionBase"; }:
  synExpr<LEFT_EXPR, LeftExpression>
  LARROW<LARROW, LArrow>
  synExpr<RIGHT_EXPR, RightExpression>;

interface indexerExpr:
  itemIndexerExpr |
  namedIndexerExpr;

itemIndexerExpr options { stubBase="FSharpExpressionBase"; } extras {
  get { methodName="Args" path=<itemIndexerExpr:ARG_LIST/indexerArgList:INDEXER_ARG> };
}:
  synExpr<EXPR, Expression>
  DOT
  indexerArgList<ARG_LIST, IndexerArgList>;

namedIndexerExpr options { stubBase="FSharpExpressionBase"; } extras {
  get { methodName="Args" path=<namedIndexerExpr:INDEXER_ARG> };
}:
  // todo: maybe: expr, dot, ident, arg?
  referenceExpr<EXPR, Expression>
  indexerArg<INDEXER_ARG, IndexerArg>;

indexerArgList:
  LBRACK
  synExpr<INDEXER_ARG, IndexerArgs>
    (
      COMMA
      synExpr<INDEXER_ARG, IndexerArgs>
    )*
  RBRACK;

indexerArg:
  synExpr<EXPR, Expressions>
  (
    DOT_DOT
    synExpr<EXPR, Expressions>
  )?;

interface castExpr:
  upcastExpr |
  downcastExpr;

parenExpr options { stubBase="FSharpExpressionBase"; }:
  LPAREN<LPAREN, LeftParen>
  synExpr<EXPR, InnerExpression>?
  RPAREN<RPAREN, RightParen>;

quoteExpr options { stubBase="FSharpExpressionBase"; }:
  (
    LQUOTE_TYPED
    synExpr<EXPR, QuotedExpression>?
    RQUOTE_TYPED?
  ) |
  (
    LQUOTE_UNTYPED
    synExpr<EXPR, QuotedExpression>?
    RQUOTE_UNTYPED?
  );

constExpr options { stubBase="FSharpExpressionBase"; }:
  fakeSeparator;

typedExpr options { stubBase="FSharpExpressionBase"; }:
  synExpr<EXPR, Expression>
  COLON
  synType<TYPE, Type>;

tupleExpr options { stubBase="FSharpExpressionBase"; }:
  (
    LPAREN
    STRUCT<STRUCT, StructKeyword>
  )?
  synExpr<EXPR, Expressions>
  (
    synExpr<EXPR, Expressions>
    COMMA
  )*
  fakeSeparator;

anonRecdExpr options { stubBase="FSharpExpressionBase"; }:
  STRUCT<STRUCT, STRUCT_KEYWORD>?
  LBRACE_BAR
  (
    synExpr<COPY_INFO, CopyInfoExpression>
    WITH
  )?
  recordExprBinding<BINDING, ExprBindings>* // todo: FCS has different type here
  BAR_RBRACE;

arrayOrListExpr options { stubBase="FSharpExpressionBase"; }:
  fakeSeparator;

recordExpr options { stubBase="FSharpExpressionBase"; }:
  LBRACE<LBRACE, LeftBrace>
  (
    synExpr<COPY_INFO, CopyInfoExpression>
    WITH
  )?
  recordExprBinding<BINDING, ExprBindings>*
  RBRACE<RBRACE, RightBrace>;

recordExprBinding:
  expressionReferenceName<REFERENCE_NAME, ReferenceName>
  EQUALS
  synExpr<EXPR, Expression>
  SEMICOLON<SEMI, Semicolon>?;

newExpr options { stubBase="FSharpExpressionBase"; }:
  NEW<NEW, NewKeyword>
  typeReferenceName<TYPE_REFERENCE, TypeName>
  synExpr<EXPR, ArgExpression>;

forExpr options { stubBase="FSharpExpressionBase"; }:
  FOR
  IDENTIFIER<IDENTIFIER, Identifier>
  synExpr<IDENT_EXPR, IdentExpression>
  TO
  synExpr<TO_EXPR, ToExpression>
  DO
  synExpr<DO_EXPR, DoExpression>;

forEachExpr options { stubBase="FSharpExpressionBase"; }:
  FOR
  synPat<PAT, Pattern>
  IN
  synExpr<IN_EXPR, InExpression>
  DO
  synExpr<DO_EXPR, DoExpression>;

arrayOrListOfSeqExpr options { stubBase="FSharpExpressionBase"; }:
  (LBRACK
   synExpr<EXPR, Expression>
   RBRACK) |

   (LBRACK_BAR
    synExpr<EXPR, Expression>
    BAR_RBRACK);

whileExpr options { stubBase="FSharpExpressionBase"; }:
  WHILE
  synExpr<WHILE_EXPR, WhileExpression>
  DO
  synExpr<DO_EXPR, DoExpression>;

objExpr options { stubBase="FSharpExpressionBase"; }:
  LBRACE
  NEW
  typeReferenceName<TYPE_REFERENCE, TypeName>
  WITH
  fakeSeparator
  RBRACE;

objExprSecondaryInterface:
  typeReferenceName<TYPE_REFERENCE, TypeName>
  fakeSeparator;

compExpr options { stubBase="FSharpExpressionBase"; }:
  LBRACE
  synExpr<EXPR, Expression>
  RBRACE;

lambdaExpr options { stubBase="FSharpExpressionBase"; }:
  FUN
  synPat<PAT, Patterns>*
  RARROW
  synExpr<EXPR, Expression>;

matchLambdaExpr options { stubBase="FSharpExpressionBase"; }:
  FUNCTION
  matchClause<MATCH_CLAUSE, clauses>*;

// todo: separate nodes for bang exprs?
matchExpr options { stubBase="FSharpExpressionBase"; }:
  MATCH
  synExpr<EXPR, Expression>
  WITH
  matchClause<MATCH_CLAUSE, Clauses>*;

doExpr options { stubBase="FSharpExpressionBase"; }:
  DO
  synExpr<EXPR, Expression>;

assertExpr options { stubBase="FSharpExpressionBase"; }:
  ASSERT
  synExpr<EXPR, Expression>;

prefixAppExpr options { stubBase="FSharpExpressionBase"; }:
  synExpr<FUNC_EXPR, FunctionExpression>
  synExpr<ARG_EXPR, ArgumentExpression>;

infixAppExpr options { stubBase="FSharpExpressionBase"; }:
  synExpr<ARG_EXPR, ArgumentExpression>
  synExpr<FUNC_EXPR, FunctionExpression>;

letOrUseExpr options { stubBase="FSharpExpressionBase"; }:
  letOrUse{LET, LetOrUseToken}
  REC<REC, RecKeyword>?
  binding<BINDING, Bindings>
  (
    AND<AND, Separators>
    binding<BINDING, Bindings>
  )*
  synExpr<BODY_EXPR, BodyExpression>;

tryWithExpr options { stubBase="FSharpExpressionBase"; }:
  TRY
  synExpr<TRY_EXPR, TryExpression>
  WITH
  matchClause<CLAUSE, WithCases>*;

tryFinallyExpr options { stubBase="FSharpExpressionBase"; }:
  TRY
  synExpr<TRY_EXPR, TryExpression>
  FINALLY
  synExpr<FINALLY_EXPR, FinallyExpression>;

lazyExpr options { stubBase="FSharpExpressionBase"; }:
  LAZY
  synExpr<EXPR, Expression>;

sequentialExpr options { stubBase="FSharpExpressionBase"; }:
  synExpr<EXPR1, Expression1>
  synExpr<EXPR2, Expression2>;

ifThenElseExpr options { stubBase="FSharpExpressionBase"; }:
  IF<IF, IfKeyword>
  synExpr<IF_EXPR, IfExpr>
  THEN<THEN, ThenKeyword>
  synExpr<THEN_EXPR, ThenExpr>
  (
    ELSE<ELSE, ElseKeyword>
    synExpr<ELSE_EXPR, ElseExpr>
  )?;

referenceExpr options { stubBase="FSharpExpressionBase"; } extras {
  get { methodName="TypeArguments" path=<referenceExpr:TYPE_ARG_LIST/prefixAppTypeArgumentList:TYPE> };
}:
  (
    synExpr<QUALIFIER_EXPR, Qualifier>
    DOT
  )?
  fSharpIdentifier<IDENTIFIER, Identifier>
  prefixAppTypeArgumentList<TYPE_ARG_LIST, TypeArgumentsList>?;

matchClause options { stubBase="FSharpExpressionBase"; }:
  BAR<BAR, Bar>?
  synPat<PAT, Pattern>
  RARROW<RARROW, RArrow>
  synExpr<EXPR, Expression>;

typeTestExpr options { stubBase="FSharpExpressionBase"; }:
  synExpr<EXPR, Expression>
  COLON_QMARK
  synType<TYPE, Type>;

upcastExpr options { stubBase="FSharpExpressionBase"; }:
  synExpr<EXPR, Expression>
  COLON_GREATER<OP, OperatorToken>
  synType<TYPE, Type>;
  
downcastExpr options { stubBase="FSharpExpressionBase"; }:
  synExpr<EXPR, Expression>
  COLON_QMARK_GREATER
  synType<TYPE, Type>;

inferredCastExpr options { stubBase="FSharpExpressionBase"; }:
  (UPCAST | DOWNCAST)
  synExpr<EXPR, Expression>;

// todo: maybe replace with SynExpr.Const in FCS?
nullExpr options { stubBase="FSharpExpressionBase"; }:
  NULL<NULL, NullKeyword>;

addressOfExpr options { stubBase="FSharpExpressionBase"; }:
  (AMP | AMP_AMP)
  synExpr<EXPR, Expression>;

traitCallExpr options { stubBase="FSharpExpressionBase"; }:
  fakeSeparator;

joinInExpr options { stubBase="FSharpExpressionBase"; }:
  fakeSeparator;

implicitZeroExpr options { stubBase="FSharpExpressionBase"; }:
  fakeSeparator;

yieldOrReturnExpr options { stubBase="FSharpExpressionBase"; }:
  (YIELD | YIELD_BANG | RETURN | RETURN_BANG)
  synExpr<EXPR, Expression>;

letOrUseBangExpr options { stubBase="FSharpExpressionBase"; }:
  letOrUseBang{LET, LetOrUseToken}
  synPat<PAT, Pattern>
  EQUALS<EQUALS, EqualsToken>
  synExpr<EXPR, Expression>
  IN?
  synExpr<IN_EXPR, InExpression>;

libraryOnlyExpr options { stubBase="FSharpExpressionBase"; }:
  fakeSeparator;

fromErrorExpr options { stubBase="FSharpExpressionBase"; }:
  fakeSeparator;

fixedExpr options { stubBase="FSharpExpressionBase"; }:
  FIXED<FIXED, FixedKeyword>
  synExpr<EXPR, Expression>;

chameleonExpression options { stubBase="FSharpExpressionBase"; }:
  synExpr<EXPR, Expression>;


interface fSharpTypeDeclaration:
  exceptionDeclaration |
  simpleTypeDeclaration |
  typeExtensionDeclaration |
  delegateDeclaration |
  objectModelTypeDeclaration |
  nestedTypeUnionCaseDeclaration;

interface simpleTypeDeclaration:
  enumDeclaration |
  recordDeclaration |
  unionDeclaration |
  notCompiledTypeDeclaration;

interface notCompiledTypeDeclaration:
  typeAbbreviationDeclaration |
  moduleAbbreviation |
  abstractTypeDeclaration |
  otherSimpleTypeDeclaration;

interface objectModelTypeDeclaration:
  classDeclaration |
  interfaceDeclaration |
  structDeclaration |
  objectTypeDeclaration;

nestedModuleDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; }:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>
  moduleMember<MODULE_MEMBER, Members>*;

typeExtensionDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
   get { methodName="TypeParameters" path=<typeExtensionDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
   get { methodName="TypeMembers" path=<typeExtensionDeclaration:TYPE_MEMBER> };
 }:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  longIdentifier<IDENTIFIER, LongIdentifier>
  namedType<EXTENDED_TYPE, ExtendedType>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;

// Enums

enumDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<enumDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  enumMemberDeclaration<ENUM_MEMBER, EnumMembers>*
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;

enumMemberDeclaration options { stubBase="FSharpProperTypeMemberDeclarationBase"; }:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  IDENTIFIER<IDENTIFIER, Identifier>;



// Records

recordDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<recordDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  recordFieldDeclaration<RECORD_FIELD, Fields>*
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;


recordFieldDeclaration options { stubBase="FSharpProperTypeMemberDeclarationBase"; }:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  IDENTIFIER<IDENTIFIER, Identifier>
  COLON
  synType<TYPE, Type>;



// Discriminated unions

unionCasesList:
  modifier{ACCESS_MODIFIER, Modifier}?
  unionCaseDeclaration<UNION_CASE, UnionCases>*;

unionDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<unionDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
  get { methodName="UnionCases" path=<unionDeclaration:UNION_CASES_LIST/unionCasesList:UNION_CASE> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  unionCasesList<UNION_CASES_LIST, UnionCasesList>
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;


interface unionCaseDeclaration:
  singletonCaseDeclaration |
  nestedTypeUnionCaseDeclaration;

singletonCaseDeclaration options { stubBase="FSharpProperTypeMemberDeclarationBase"; }:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>;

nestedTypeUnionCaseDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<nestedTypeUnionCaseDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>
  caseFieldDeclaration<UNION_FIELD, Fields>*
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;


interface caseFieldDeclaration:
  unionCaseFieldDeclaration |
  exceptionFieldDeclaration;

unionCaseFieldDeclaration options { stubBase="UnionCaseFieldDeclarationBase"; }:
  IDENTIFIER<IDENTIFIER, Identifier>?
  synType<TYPE, Type>;

exceptionFieldDeclaration options { stubBase="UnionCaseFieldDeclarationBase"; }:
  IDENTIFIER<IDENTIFIER, Identifier>?
  synType<TYPE, Type>;


// Other members

exceptionDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<exceptionDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  caseFieldDeclaration<UNION_FIELD, Fields>*
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;

typeAbbreviationDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<typeAbbreviationDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  EQUALS
  synType<TYPE, AbbreviatedType>
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;

moduleAbbreviation options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<moduleAbbreviation:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;

interface activePatternCaseDeclaration:
  activePatternNamedCaseDeclaration |
  activePatternWildCase;

interface activePatternNamedCaseDeclaration:
  topActivePatternCaseDeclaration |
  localActivePatternCaseDeclaration;

topActivePatternCaseDeclaration options { stubBase="FSharpDeclarationBase"; }:
   IDENTIFIER<IDENTIFIER, Identifier>;

localActivePatternCaseDeclaration options { stubBase="LocalDeclarationBase"; }:
  IDENTIFIER<IDENTIFIER, Identifier>;

activePatternWildCase:
  wildPat;


otherMemberDeclaration options { stubBase="DummyDeclaration"; }:
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>;

otherSimpleTypeDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<otherSimpleTypeDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;

abstractTypeDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<abstractTypeDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;


// Object model type declarations

classDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<classDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;

interfaceDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<interfaceDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;

structDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<structDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;

objectTypeDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<objectTypeDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;

delegateDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<delegateDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>
  IDENTIFIER<IDENTIFIER, Identifier>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;

interface fSharpTypeMemberDeclaration:
  constructorDeclaration |
  inheritMember |
  interfaceImplementation |
  abstractSlot |
  memberDeclaration |
  typeLetBindings |
  valField |
  autoProperty |
  letModuleDecl |

  otherTypeMember;

interface constructorDeclaration:
  implicitConstructorDeclaration |
  memberConstructorDeclaration;

interface inheritMember:
  typeInherit |
  interfaceInherit;

interfaceInherit:
  INHERIT
  typeReferenceName<TYPE_REFERENCE, TypeName>;

typeInherit options { stubBase="ReferenceOwnerBase"; }:
  INHERIT
  typeReferenceName<TYPE_REFERENCE, TypeName>
  chameleonExpression<CHAMELEON_EXPR, CtorArg>;

interfaceImplementation:
  INTERFACE
  typeReferenceName<TYPE_REFERENCE, TypeName>
  (
    WITH
    fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*
  )?;


otherTypeMember: fakeSeparator;
typeLetBindings: fakeSeparator;

implicitConstructorDeclaration options { stubBase="FSharpProperTypeMemberDeclarationBase"; }:
  memberParam<PARAMETER, Parameters>
  (
    memberParam<PARAMETER, Parameters>
    COMMA
  )*
  ctorSelfId<SELF_ID, SelfIdentifier>?;

interface selfId:
  ctorSelfId |
  memberSelfId;

ctorSelfId options { stubBase="LocalDeclarationBase"; }:
  AS
  IDENTIFIER<IDENTIFIER, Identifier>;

memberSelfId options { stubBase="LocalDeclarationBase"; }:
  IDENTIFIER<IDENTIFIER, Identifier>;  

memberConstructorDeclaration options { stubBase="FSharpProperTypeMemberDeclarationBase"; }:
  NEW<NEW, NewKeyword>
  localDeclaration<PARAMETER, Parameters>* fakeSeparator
  ctorSelfId<SELF_ID, SelfIdentifier>?;



// todo: fix properties with explicit setters

memberDeclaration options { stubBase="FSharpProperTypeMemberDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<memberDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfMethodList:TYPE_PARAMETER> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  accessModifiers<ACCESS_MODIFIERS, AccessModifiers>?
  memberSelfId<SELF_ID, SelfId>?
  identOrOpName{IDENTIFIER, Identifier}
  typeParameterOfMethodList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  (
    memberParam<PARAMETER, Parameters>*
    EQUALS
    chameleonExpression
  |
    WITH
    accessorDeclaration
    (
      AND
      accessorDeclaration
    )
  );

abstractSlot options { stubBase="FSharpProperTypeMemberDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<abstractSlot:TYPE_PARAMETERS_LIST/typeParameterOfMethodList:TYPE_PARAMETER> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  IDENTIFIER<IDENTIFIER, Identifier>
  typeParameterOfMethodList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  memberParam<PARAMETER, Parameters>*;

valField options { stubBase="FSharpProperTypeMemberDeclarationBase"; }:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  IDENTIFIER<IDENTIFIER, Identifier>;

autoProperty options { stubBase="FSharpProperTypeMemberDeclarationBase"; }  extras {
  get { methodName="Expression" path=<autoProperty:CHAMELEON_EXPR/chameleonExpression:EXPR> };
}:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  IDENTIFIER<IDENTIFIER, Identifier>
  EQUALS
  chameleonExpression<CHAMELEON_EXPR, InitExpression>
  accessorsNamesClause<ACESSORS_CLAUSE, AccessorsClause>?;

accessorsNamesClause:
  WITH
  (
    IDENTIFIER<ACESSOR, AccessorsNames>
  |
    IDENTIFIER<ACESSOR, AccessorsNames>
    COMMA
    IDENTIFIER<ACESSOR, AccessorsNames>
  );

accessorDeclaration:
  IDENTIFIER<IDENTIFIER, Identifier>
  memberParam<PARAMETER, Parameters>*
  EQUALS
  chameleonExpression<CHAMELEON_EXPR, BodyExpression>;

memberParam:
  synPat<PATTERN, Pattern>;


// module statements

interface hashDirective:
  lineDirective |
  loadDirective |
  referenceDirective |
  iDirective |
  otherDirective;

lineDirective:
  PP_LINE<PP_LINE, HashToken>
  STRING<STRING, Args>*;

loadDirective:
  PP_LOAD<PP_LOAD, HashToken>
  STRING<STRING, Args>*;

referenceDirective:
  PP_REFERENCE<PP_REFERENCE, HashToken>
  STRING<STRING, Args>*;

iDirective:
  PP_I<PP_I, HashToken>
  STRING<STRING, Args>*;

otherDirective:
  PP_DIRECTIVE<PP_DIRECTIVE, HashToken>
  STRING<STRING, Args>*;

openStatement  options { stubBase="TypeReferenceOwnerBase"; }:
  OPEN<OPEN, Open>
  typeReferenceName<TYPE_REFERENCE, ReferenceName>;

do options { stubBase="DummyDeclaration"; } extras {
    get { methodName="Expression" path=<do:CHAMELEON_EXPR/chameleonExpression:EXPR> };
}:
  DO<DO, DoKeyword>?
  chameleonExpression<CHAMELEON_EXPR, DoExpression>;

otherModuleMember: fakeSeparator;



// Identifiers

private letOrUse{ROLE, Getter}:
  LET<@ROLE, @Getter> |
  USE<@ROLE, @Getter>;

private letOrUseBang{ROLE, Getter}:
  LET_BANG<@ROLE, @Getter> |
  USE_BANG<@ROLE, @Getter>;

private identOrOpName{ROLE, Getter}:
  IDENTIFIER<@ROLE, @Getter> |
  SYMBOLIC_OP<@ROLE, @Getter> |
  activePatternId<@ROLE, @Getter> | 
  longIdentifier<@ROLE, @Getter>;

activePatternId:
  LPAREN<LPAREN, LParen>
  BAR
  (
    activePatternCaseDeclaration<ACTIVE_PATTERN_CASE, Cases>
    BAR
  )+
  RPAREN<RPAREN, RParen>;

longIdentifier:
  IDENTIFIER
  (
    DOT
    IDENTIFIER<IDENTIFIER, Identifiers>
  )*;

interface fSharpIdentifier:
  identifier |
  symbolicOp |
  activePatternId;

abstract identifier options { interfaceName="IFSharpIdentifier"; }:
  IDENTIFIER;

abstract symbolicOp options { interfaceName="IFSharpIdentifier"; }:
  SYMBOLIC_OP;

// Type parameters

typeParameterOfTypeList:
  LESS<LESS,LAngle>
  typeParameterOfTypeDeclaration<TYPE_PARAMETER, TypeParameters>*
  GREATER<GREATER,RAngle>;

typeParameterOfMethodList:
  LESS<LESS,LAngle>
  typeParameterOfMethodDeclaration<TYPE_PARAMETER, TypeParameters>*
  GREATER<GREATER,RAngle>;

typeParameterOfTypeDeclaration options { stubBase="FSharpDeclarationBase"; }:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  IDENTIFIER<IDENTIFIER, Identifier>;

typeParameterOfMethodDeclaration options { stubBase="FSharpDeclarationBase"; }:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  IDENTIFIER<IDENTIFIER, Identifier>;


// Access modifiers

accessModifiers: modifier{ACCESS_MODIFIER, Modifiers}*;

private modifier{ROLE,Getter}:
  PUBLIC<@ROLE,@Getter> |
  PRIVATE<@ROLE,@Getter> |
  INTERNAL<@ROLE,@Getter>;



// Attributes

fSharpAttribute options { stubBase="TypeReferenceOwnerBase"; }
  extras {
    get { methodName="Expression" path=<fSharpAttribute:CHAMELEON_EXPR/chameleonExpression:EXPR> };
  }:
  attributeTarget<ATTR_TARGET, Target>?
  typeReferenceName<REFERENCE_NAME, ReferenceName>
  chameleonExpression<CHAMELEON_EXPR, ArgExpression>?;

attributeTarget:
  IDENTIFIER<IDENTIFIER, Identifier>
  COLON;


fakeSeparator: FAKE;

// Local declarations

localDeclaration options { stubBase="LocalDeclarationBase"; }:
  fSharpAttribute<ATTRIBUTE, Attributes>*
  identOrOpName{IDENTIFIER, Identifier};


interface referenceName:
  expressionReferenceName |
  typeReferenceName;

interface typeArgumentList:
  prefixAppTypeArgumentList |
  postfixAppTypeArgumentList;


postfixAppTypeArgumentList:
  (
    synType<TYPE, Types>
  |
    LPAREN
    synType<TYPE, Types>
    (
      COMMA
      synType<TYPE, Types>
    )*
    RPAREN
 );

prefixAppTypeArgumentList:
  LESS<LESS,LAngle>
  synType<TYPE, Types>
  (
    COMMA
    synType<TYPE, Types>
  )*
  GREATER<GREATER,RAngle>;


expressionReferenceName options { stubBase="ReferenceOwnerBase"; }:
  (
    expressionReferenceName<QUALIFIER, Qualifier>
    DOT<DELIMITER, Dot>
  )?
  fSharpIdentifier<IDENTIFIER, Identifier>
  prefixAppTypeArgumentList<TYPE_ARG, TypeArgumentList>;


typeReferenceName options { stubBase="TypeReferenceOwnerBase"; }:
  (
    postfixAppTypeArgumentList<TYPE_ARG, TypeArgumentList>
    (
      typeReferenceName<QUALIFIER, Qualifier>
      DOT<DELIMITER, Dot>
    )?
    fSharpIdentifier<IDENTIFIER, Identifier>
  |
    (
      typeReferenceName<QUALIFIER, Qualifier>
      DOT<DELIMITER, Dot>
    )?
    fSharpIdentifier<IDENTIFIER, Identifier>
    prefixAppTypeArgumentList<TYPE_ARG, TypeArgumentList>
  );


interface synType:
  namedType |
  tupleType |
  anonRecordType |
  arrayType |
  funType |
  varType |
  anonType |
  otherType;


namedType:
  typeReferenceName<TYPE_REFERENCE, ReferenceName>;

tupleType:
  synType<ITEM, Items>
  (
    STAR
    synType<ITEM, Items>
  )*;

anonRecordType:
  LBRACE_BAR
  anonRecordField<FIELD, Fields>*
  BAR_RBRACE;

anonRecordField:
  IDENTIFIER<IDENTIFIER, Identifier>
  COLON
  synType<TYPE, Type>;

arrayType:
  synType<TYPE, Type>
  LBRACK
  RBRACK;

funType:
  synType
  RARROW
  synType;

varType options { stubBase="TypeReferenceOwnerBase"; }:
  QUOTE
  IDENTIFIER<IDENTIFIER, Identifier>;

anonType:
  UNDERSCORE;

otherType:
  fakeSeparator;
