options {
  parserPackage="JetBrains.ReSharper.Plugins.FSharp.Psi.Gen";
  parserClassName="FSharpParserGenerated";
  psiInterfacePackageName="JetBrains.ReSharper.Plugins.FSharp.Psi.Tree";
  psiStubsPackageName="JetBrains.ReSharper.Plugins.FSharp.Psi.Impl.Tree";
  psiStubsBaseClass="JetBrains.ReSharper.Plugins.FSharp.Psi.Impl.Tree.FSharpCompositeElement";
  tokenClassName="JetBrains.ReSharper.Plugins.FSharp.Psi.Impl.Tree.FSharpToken";
  tokenTypePackageName="JetBrains.ReSharper.Plugins.FSharp.Psi.Parsing";
  tokenTypeClassName="FSharpTokenType";
  parserTargetSubdir="Parsing/FSharp";
  psiInterfacesTargetSubdir="Psi/FSharp";
  psiStubsTargetSubdir="Psi/FSharp/Impl";
  elementTypePrefix="";
  visitorClassName="TreeNodeVisitor";
  visitorMethodSuffix="";
  disableReflection;
  separateHierarchies;
  acceptModifier="";
  visitorSuperClassName="JetBrains.ReSharper.Plugins.FSharp.Psi.TreeNodeVisitorBase";
  "treeElementClassFQName"="JetBrains.ReSharper.Psi.ExtensionsAPI.Tree.TreeElement";
  "leafElementClassFQName"="JetBrains.ReSharper.Plugins.FSharp.Psi.Impl.Tree.FSharpLeafElement";
  "compositeElementClassFQName"="JetBrains.ReSharper.Plugins.FSharp.Psi.Impl.Tree.FSharpCompositeElement";
  "psiElementVisitorClassFQName"="JetBrains.ReSharper.Plugins.FSharp.Psi.Tree.TreeNodeVisitor";
  "unexpectedTokenClassFQName"="Parsing.UnexpectedToken";
  "syntaxErrorClassFQName"="Parsing.SyntaxError";
  "parserGenRuntimePackageName"="Parsing";
  "createTokenElementMethodFQName"="JetBrains.ReSharper.Plugins.FSharp.Psi.Impl.Factory.createTokenElement";
  "visitMethodPrefix"="Visit";
  "lexerClassName"="JetBrains.ReSharper.Plugins.FSharp.Psi.Parsing.FSharpLexer";
  "psiClassesPrefix"="";
  "psiElementClassFQName"="JetBrains.ReSharper.Plugins.FSharp.Psi.Tree.IFSharpTreeNode";
  customImplPackage="JetBrains.ReSharper.Plugins.FSharp.Psi.Impl.Tree";
  customInterfacePackage="JetBrains.ReSharper.Plugins.FSharp.Psi.Tree";
  "interfaceNamePrefix"="I";
  "tokenElementClassFQName"="JetBrains.ReSharper.Psi.Tree.ITokenNode";
  "customImplSuffix"="";
  "objectClassFQName"="System.Object";
  publicChildRolePrefix="FSHARP_";
  tokenBitsetThreshold=4;
  elementTypeBaseClass="JetBrains.ReSharper.Plugins.FSharp.Psi.Tree.FSharpCompositeNodeType";
  parserMessagesClass="JetBrains.ReSharper.Plugins.FSharp.Psi.Parsing.ParserMessages";
  generateWorkingPsi;
}



// Files

interface fSharpFile options { customParseFunction; noInternalParseFunction; }:
  fSharpImplFile |
  fSharpSigFile;

fSharpImplFile options { customParseFunction; noInternalParseFunction; stubBase="FSharpFileBase"; }:
  (
    topLevelModuleLikeDeclaration<MODULE_OR_NAMESPACE_DECLARATION, ModuleDeclarations>*
  |
    anonModuleDeclaration<MODULE_OR_NAMESPACE_DECLARATION, ModuleDeclarations>
  );

fSharpSigFile options { stubBase="FSharpFileBase"; }:
  topLevelModuleLikeDeclaration<MODULE_OR_NAMESPACE_DECLARATION, ModuleDeclarations>*;


// Modules and namespaces

// todo: rewrite to make anon module top level module interface

interface moduleLikeDeclaration:
  anonModuleDeclaration
  declaredModuleLikeDeclaration;

interface declaredModuleLikeDeclaration:
  topLevelModuleLikeDeclaration |
  nestedModuleDeclaration;

interface topLevelModuleLikeDeclaration:
  qualifiableModuleLikeDeclaration |
  globalNamespaceDeclaration;

interface qualifiableModuleLikeDeclaration:
  namedModuleDeclaration |
  namedNamespaceDeclaration;

interface qualifiableDeclaration:
  qualifiableModuleLikeDeclaration |
  typeExtensionDeclaration;

interface moduleDeclaration:
  anonModuleDeclaration |
  declaredModuleDeclaration;

interface declaredModuleDeclaration:
  namedModuleDeclaration |
  nestedModuleDeclaration;

namedNamespaceDeclaration options { stubBase="FSharpDeclarationBase"; }:
  NAMESPACE<NAMESPACE, ModuleOrNamespaceKeyword>
  REC<REC, RecKeyword>?
  (
    referenceName<REFERENCE_NAME, QualifierReferenceName>
    DOT<FSHARP_DELIMITER, Delimiter>
  )?
  fSharpIdentifier<IDENTIFIER, Identifier>
  moduleMember<MODULE_MEMBER, Members>*;

globalNamespaceDeclaration options { stubBase="FSharpDeclarationBase"; }:
  NAMESPACE<NAMESPACE, ModuleOrNamespaceKeyword>
  REC<REC, RecKeyword>?
  GLOBAL<GLOBAL, GlobalKeyword>
  moduleMember<MODULE_MEMBER, Members>*;

namedModuleDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="Attributes" path=<namedModuleDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  MODULE<MODULE, ModuleOrNamespaceKeyword>
  REC<REC, RecKeyword>?
  accessModifier{ACCESS_MODIFIER, AccessModifier}?
  (
    referenceName<REFERENCE_NAME, QualifierReferenceName>
    DOT<FSHARP_DELIMITER, Delimiter>
  )?
  fSharpIdentifier<IDENTIFIER, Identifier>
  moduleMember<MODULE_MEMBER, Members>*;

anonModuleDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; }:
  moduleMember<MODULE_MEMBER, Members>*;



// Module members

interface moduleMember:
  nestedModuleDeclaration |
  typeDeclarationGroup |
  exceptionDeclaration |
  letBindingsDeclaration |
  externDeclaration |
  moduleAbbreviationDeclaration |
  openStatement |
  doStatement |
  hashDirective |
  bindingSignature;

letBindingsDeclaration options { stubBase="DummyDeclaration"; } extras {
  get { methodName="Attributes" path=<letBindingsDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  STATIC<STATIC, StaticKeyword>?
  letOrUse{LET, BindingKeyword}
  INLINE<INLINE, InlineKeyword>?
  REC<REC, RecKeyword>?
  LIST binding<BINDING, Bindings> SEP AND<FSHARP_AND, Separators>;

bindingSignature extras {
  get { methodName="Attributes" path=<bindingSignature:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
  get { methodName="Expression" path=<bindingSignature:CHAMELEON_EXPR/chameleonExpression:EXPR> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  VAL<VAL, BindingKeyword>
  INLINE<INLINE, InlineKeyword>?
  MUTABLE<MUTABLE, MutableKeyword>?
  referencePat<HEAD_PATTERN, HeadPattern>
  returnTypeInfo<RETURN_INFO, ReturnTypeInfo>
  (
    EQUALS<EQUALS, EqualsToken>
    chameleonExpression<CHAMELEON_EXPR, ChameleonExpression>
  )?;

interface letBindings:
  letBindingsDeclaration |
  letOrUseExpr;

interface binding:
  bindingImplementation
  bindingSignature;

interface bindingImplementation:
  topBinding |
  localBinding;

topBinding extras {
  get { methodName="Expression" path=<topBinding:CHAMELEON_EXPR/chameleonExpression:EXPR> };
  get { methodName="Attributes" path=<topBinding:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  INLINE<INLINE, InlineKeyword>?
  MUTABLE<MUTABLE, MutableKeyword>?
  fSharpPattern<HEAD_PATTERN, HeadPattern>
  returnTypeInfo<RETURN_INFO, ReturnTypeInfo>?
  EQUALS<EQUALS, EqualsToken>
  chameleonExpression<CHAMELEON_EXPR, ChameleonExpression>;

localBinding extras {
  get { methodName="Attributes" path=<localBinding:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  INLINE<INLINE, InlineKeyword>?
  MUTABLE<MUTABLE, MutableKeyword>?
  fSharpPattern<HEAD_PATTERN, HeadPattern>
  returnTypeInfo<RETURN_INFO, ReturnTypeInfo>?
  EQUALS<EQUALS, EqualsToken>
  fSharpExpression<EXPR, Expression>;

returnTypeInfo extras {
  get { methodName="Attributes" path=<returnTypeInfo:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  COLON<FSHARP_COLON, Colon>
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  typeUsage<TYPE, ReturnType>;

externDeclaration:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  EXTERN
  accessModifier{ACCESS_MODIFIER, AccessModifier}?
  fSharpIdentifier<IDENTIFIER, Identifier>;
//   cLikeParameterList<PARAMETER_LIST, ParameterList>;

// cLikeParameterList:
//   LPAREN
//   fakeSeparator
//   RPAREN;

interface fSharpPattern:
  namedPat |
  typedPat |
  attribPat |
  orPat |
  andsPat |
  tuplePat |
  arrayOrListPat |
  parenPat |
  recordPat |
  optionalValPat |
  isInstPat |
  listPat |
  listConsPat |
  wildPat |
  constPat |
  fieldPat |
  otherPat;

interface namedPat:
  referencePat |
  parametersOwnerPat |
  asPat;

interface referencePat:
  localReferencePat |
  topReferencePat;

// todo: fix active pattern and operator decls
interface parametersOwnerPat:
  localParametersOwnerPat
  topParametersOwnerPat;

interface asPat:
  localAsPat |
  topAsPat;


localReferencePat options { stubBase="LocalPatternDeclarationBase"; }extras {
  get { methodName="Identifier" path=<localReferencePat:REFERENCE_NAME/expressionReferenceName:IDENTIFIER> };
}:
  expressionReferenceName<REFERENCE_NAME, ReferenceName>;

topReferencePat options { stubBase="TopPatternDeclarationBase"; } extras {
  get { methodName="Identifier" path=<topReferencePat:REFERENCE_NAME/expressionReferenceName:IDENTIFIER> };
}:
  expressionReferenceName<REFERENCE_NAME, ReferenceName>;

localParametersOwnerPat options { stubBase="LocalPatternDeclarationBase"; } extras {
  get { methodName="Identifier" path=<localParametersOwnerPat:REFERENCE_NAME/expressionReferenceName:IDENTIFIER> };
}:
  expressionReferenceName<REFERENCE_NAME, ReferenceName>
  fSharpPattern<PARAMETER, Parameters>*;

topParametersOwnerPat options { stubBase="TopPatternDeclarationBase"; } extras {
  get { methodName="Identifier" path=<topParametersOwnerPat:REFERENCE_NAME/expressionReferenceName:IDENTIFIER> };
}:
  expressionReferenceName<REFERENCE_NAME, ReferenceName>
  fSharpPattern<PARAMETER, Parameters>*;

localAsPat options { stubBase="LocalPatternDeclarationBase"; }:
  fSharpPattern<PATTERN, Pattern>
  AS
  fSharpIdentifier<IDENTIFIER, Identifier>;

topAsPat options { stubBase="TopPatternDeclarationBase"; }:
  fSharpPattern<PATTERN, Pattern>
  AS
  fSharpIdentifier<IDENTIFIER, Identifier>;


interface arrayOrListPat:
  arrayPat |
  listPat;

typedPat options { stubBase="FSharpPatternBase"; }:
  fSharpPattern<PATTERN, Pattern>
  COLON<FSHARP_COLON, Colon>
  typeUsage<TYPE, Type>;

attribPat options { stubBase="FSharpPatternBase"; } extras {
  get { methodName="Attributes" path=<attribPat:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  fSharpPattern<PATTERN, Pattern>;

orPat options { stubBase="FSharpPatternBase"; }:
  fSharpPattern<PATTERN1, Pattern1>
  BAR
  fSharpPattern<PATTERN2, Pattern2>;

andsPat options { stubBase="FSharpPatternBase"; }:
  fSharpPattern<PATTERN1, Pattern1>
  AMP
  fSharpPattern<PATTERN2, Pattern2>;

arrayPat options { stubBase="FSharpPatternBase"; }:
  LBRACK_BAR
  fSharpPattern<ATTRIBUTE, Patterns>*
  LBRACK_BAR;

listPat options { stubBase="FSharpPatternBase"; }:
  LBRACK<LBRACE, LeftBracket>
  fSharpPattern<ATTRIBUTE, Patterns>*
  RBRACK<RBRACE, RightBracket>;

tuplePat options { stubBase="FSharpPatternBase"; }:
  STRUCT<STRUCT, StructKeyword>?
  LIST fSharpPattern<ATTRIBUTE, Patterns> SEP COMMA<FSHARP_COMMA, Commas>;

parenPat options { stubBase="FSharpPatternBase"; }:
  LPAREN<LPAREN, LeftParen>
  fSharpPattern<PATTERN, Pattern>
  RPAREN<RPAREN, RightParen>;

recordPat options { stubBase="FSharpPatternBase"; }:
  LBRACE
  fieldPat<FIELD_PAT, FieldPatterns>*
  RBRACE;

fieldPat options { stubBase="FSharpPatternBase"; }:
  expressionReferenceName<REFERENCE_NAME, ReferenceName>
  EQUALS<FSHARP_EQUALS, EqualsToken>
  fSharpPattern<PAT, Pattern>;

optionalValPat options { stubBase="FSharpPatternBase"; }:
  QMARK
  fSharpPattern<PATTERN, Pattern>;

isInstPat options { stubBase="FSharpPatternBase"; }:
  COLON_QMARK
  typeUsage<SYN_TYPE, Type>;

listConsPat options { stubBase="FSharpPatternBase"; }:
  fSharpPattern<PATTERN1, HeadPattern>
  COLON_COLON
  fSharpPattern<PATTERN2, TailPattern>;

wildPat options { stubBase="LocalPatternDeclarationBase"; }:
  UNDERSCORE<UNDERSCORE, Underscore>;


interface constPat:
  unitPat |
  literalPat;

literalPat options { stubBase="FSharpPatternBase"; }:
  (
    TRUE |
    FALSE |
    STRING // todo: add more
  );

unitPat options { stubBase="FSharpPatternBase"; }:
  LPAREN<LPAREN, LParen>
  RPAREN<RPAREN, RParen>;


otherPat options { stubBase="FSharpPatternBase"; }:
  fakeSeparator;


interface fSharpExpression:
  parenExpr |
  quoteExpr |
  constExpr |
  typedLikeExpr |
  tupleExpr |
  recordLikeExpr |
  arrayOrListExpr |
  objExpr |
  forLikeExpr |
  rangeSequenceExpr |
  computationExpr |
  lambdaExpr |
  matchLikeExpr |
  doLikeExpr |
  appLikeExpr |
  binaryAppExpr |
  letOrUseExpr |
  tryLikeExpr |
  sequentialExpr |
  conditionOwnerExpr |
  qualifiedExpr |
  setExpr |
  nullExpr |
  addressOfExpr |
  traitCallExpr |
  joinInExpr |
  implicitZeroExpr |
  yieldOrReturnExpr |

  // todo: cover cases
  interpolatedStringExpr |
  libraryOnlyExpr |
  fromErrorExpr;

interface doLikeExpr:
  doExpr |
  assertExpr |
  lazyExpr |
  fixedExpr |
  inferredCastExpr;

interface forLikeExpr:
  forExpr |
  forEachExpr;

interface appLikeExpr:
  prefixAppExpr |
  newExpr;

interface appExpr:
  prefixAppExpr |
  binaryAppExpr;

interface typedLikeExpr:
  typedExpr |
  typeTestExpr |
  castExpr;

interface qualifiedExpr:
  referenceExpr |
  indexerExpr;

interface ifExpr:
  ifThenElseExpr |
  elifExpr;

interface conditionOwnerExpr:
  ifExpr |
  whileExpr;

setExpr options { stubBase="UnitExpressionBase"; }:
  fSharpExpression<LEFT_EXPR, LeftExpression>
  LARROW<LARROW, LArrow>
  fSharpExpression<RIGHT_EXPR, RightExpression>;

interface indexerExpr:
  itemIndexerExpr |
  namedIndexerExpr;

itemIndexerExpr options { stubBase="FSharpExpressionBase"; } extras {
  get { methodName="Args" path=<itemIndexerExpr:ARG_LIST/indexerArgList:INDEXER_ARG> };
}:
  fSharpExpression<QUALIFIER_EXPR, Qualifier>
  DOT<FSHARP_DELIMITER, Delimiter>
  indexerArgList<ARG_LIST, IndexerArgList>;

namedIndexerExpr options { stubBase="FSharpExpressionBase"; } extras {
  // todo: fix this, it's always empty now
  get { methodName="Args" path=<namedIndexerExpr:INDEXER_ARG> };
}:
  // todo: maybe: refExpr?
  fSharpExpression<QUALIFIER_EXPR, Qualifier>
  indexerArg<INDEXER_ARG, IndexerArg>;

indexerArgList:
  LBRACK
  LIST indexerArg<INDEXER_ARG, IndexerArgs> SEP COMMA<FSHARP_COMMA, Commas>
  RBRACK;

interface indexerArg:
  indexerArgExpr |
  indexerArgRange;

indexerArgExpr  extras {
  get { methodName="Expressions" path=<indexerArgExpr:EXPR> };
}:
  fSharpExpression<EXPR, Expression>;

indexerArgRange:
  fSharpExpression<EXPR, Expressions>
  DOT_DOT
  fSharpExpression<EXPR, Expressions>;

interface castExpr:
  upcastExpr |
  downcastExpr;

parenExpr options { stubBase="FSharpExpressionBase"; }:
  LPAREN<LPAREN, LeftParen>
  fSharpExpression<EXPR, InnerExpression>
  RPAREN<RPAREN, RightParen>;

quoteExpr options { stubBase="FSharpExpressionBase"; }:
  (
    LQUOTE_TYPED
    fSharpExpression<EXPR, QuotedExpression>?
    RQUOTE_TYPED?
  ) |
  (
    LQUOTE_UNTYPED
    fSharpExpression<EXPR, QuotedExpression>?
    RQUOTE_UNTYPED?
  );

interface constExpr:
  unitExpr |
  literalExpr;

unitExpr options { stubBase="UnitExpressionBase"; }:
  LPAREN<LPAREN, LParen>
  RPAREN<RPAREN, RParen>;

literalExpr options { stubBase="FSharpExpressionBase"; }:
  TRUE<LITERAL, Literal> |
  FALSE<LITERAL, Literal> |
  NULL<LITERAL, Literal> |
  SBYTE<LITERAL, Literal> |
  BYTE<LITERAL, Literal> |
  INT16<LITERAL, Literal> |
  UINT16<LITERAL, Literal> |
  INT32<LITERAL, Literal> |
  UINT32<LITERAL, Literal> |
  INT64<LITERAL, Literal> |
  UINT64<LITERAL, Literal> |
  IEEE32<LITERAL, Literal> |
  IEEE64<LITERAL, Literal> |
  DECIMAL<LITERAL, Literal> |
  BIGNUM<LITERAL, Literal> |
  NATIVEINT<LITERAL, Literal> |
  UNATIVEINT<LITERAL, Literal> |
  CHARACTER_LITERAL<LITERAL, Literal> |
  STRING<LITERAL, Literal> |
  VERBATIM_STRING<LITERAL, Literal> |
  TRIPLE_QUOTED_STRING<LITERAL, Literal> |
  BYTEARRAY<LITERAL, Literal> |
  VERBATIM_BYTEARRAY<LITERAL, Literal> |
  BYTECHAR<LITERAL, Literal>;

typedExpr options { stubBase="FSharpExpressionBase"; }:
  fSharpExpression<EXPR, Expression>
  COLON<FSHARP_COLON, Colon>
  typeUsage<TYPE, Type>;

tupleExpr options { stubBase="FSharpExpressionBase"; }:
  (
    LPAREN
    STRUCT<STRUCT, StructKeyword>
  )?
  LIST fSharpExpression<EXPR, Expressions> SEP COMMA<FSHARP_COMMA, Commas>;

interface recordLikeExpr:
  anonRecordExpr |
  recordExpr;

anonRecordExpr options { stubBase="FSharpExpressionBase"; } extras {
  get { methodName="FieldBindings" path=<anonRecordExpr:BINDING_LIST/recordFieldBindingList:BINDING> };
}:
  STRUCT<STRUCT, STRUCT_KEYWORD>?
  LBRACE_BAR
  (
    fSharpExpression<COPY_INFO, CopyInfoExpression>
    WITH
  )?
  recordFieldBindingList<BINDING_LIST, FieldBindingList>
  BAR_RBRACE;

arrayOrListExpr options { stubBase="FSharpExpressionBase"; }:
  (LBRACK | LBRACK_BAR)
  fSharpExpression<EXPR, Expression>
  (RBRACK | BAR_RBRACK);

recordExpr options { stubBase="FSharpExpressionBase"; } extras {
  get { methodName="FieldBindings" path=<recordExpr:BINDING_LIST/recordFieldBindingList:BINDING> };
}:
  LBRACE<LBRACE, LeftBrace>
  (
    fSharpExpression<COPY_INFO, CopyInfoExpression>
    WITH
  )?
  recordFieldBindingList<BINDING_LIST, FieldBindingList>
  RBRACE<RBRACE, RightBrace>;

recordFieldBindingList:
  recordFieldBinding<BINDING, FieldBindings>*;

recordFieldBinding:
  expressionReferenceName<REFERENCE_NAME, ReferenceName>
  EQUALS<FSHARP_EQUALS, EqualsToken>
  fSharpExpression<EXPR, Expression>
  SEMICOLON<FSHARP_SEMI, Semicolon>?;

newExpr options { stubBase="FSharpExpressionBase"; }:
  NEW<NEW, NewKeyword>
  typeReferenceName<TYPE_REFERENCE, TypeName>
  fSharpExpression<EXPR, ArgumentExpression>;

forExpr options { stubBase="UnitExpressionBase"; }:
  FOR
  localDeclaration<IDENTIFIER, Identifier>
  EQUALS<FSHARP_EQUALS, EqualsToken>
  fSharpExpression<IDENT_EXPR, IdentExpression>
  TO
  fSharpExpression<TO_EXPR, ToExpression>
  DO
  fSharpExpression<DO_EXPR, DoExpression>;

forEachExpr options { stubBase="UnitExpressionBase"; }:
  FOR
  fSharpPattern<PAT, Pattern>
  IN
  fSharpExpression<IN_EXPR, InExpression>
  forEachBodySeparator{BODY_SEPARATOR, BodySeparator}
  fSharpExpression<DO_EXPR, DoExpression>;

private forEachBodySeparator{ROLE, Getter}:
  DO<@ROLE, @Getter> |
  RARROW<@ROLE, @Getter>;

whileExpr options { stubBase="UnitExpressionBase"; }:
  WHILE<WHILE_KEYWORD, WhileKeyword>
  fSharpExpression<WHILE_EXPR, ConditionExpr>
  DO
  fSharpExpression<DO_EXPR, DoExpression>;

objExpr options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="InterfaceMembers" path=<objExpr:INTERFACE_IMPL/interfaceImplementation:MEMBER_LIST/memberDeclarationList:MEMBER_DECL> };
}:
  LBRACE
  NEW
  typeReferenceName<TYPE_REFERENCE, TypeName>
  fSharpExpression<ARG_EXPR, ArgExpression>
  WITH<WITH, WithKeyword>
  memberDeclaration<MEMBER_DECL, MemberDeclarations>*
  interfaceImplementation<INTERFACE_IMPL, InterfaceImplementations>*
  RBRACE;

computationExpr options { stubBase="FSharpExpressionBase"; }:
  LBRACE
  fSharpExpression<EXPR, Expression>
  RBRACE;

lambdaExpr options { stubBase="FSharpExpressionBase"; } extras {
  get { methodName="Patterns" path=<lambdaExpr:LAMBDA_PARAMETERS/lambdaParametersList:PAT> };
}:
  FUN
  lambdaParametersList<LAMBDA_PARAMETERS, Parameters>
  RARROW<RARROW, RArrow>
  fSharpExpression<EXPR, Expression>;
  
lambdaParametersList:
  fSharpPattern<PAT, Patterns>*;

interface matchClauseListOwner:
  matchExpr |
  matchLambdaExpr |
  tryWithExpr;

interface matchLikeExpr:
  matchExpr |
  matchLambdaExpr;

matchLambdaExpr options { stubBase="FSharpExpressionBase"; }:
  FUNCTION
  matchClause<MATCH_CLAUSE, Clauses>*;

// todo: separate nodes for bang exprs?
matchExpr options { stubBase="FSharpExpressionBase"; }:
  MATCH
  fSharpExpression<EXPR, Expression>
  WITH<WITH, WithKeyword>
  matchClause<MATCH_CLAUSE, Clauses>*;

doExpr options { stubBase="UnitExpressionBase"; }:
  DO<KEYWORD, Keyword>
  fSharpExpression<EXPR, Expression>;

assertExpr options { stubBase="FSharpExpressionBase"; }:
  ASSERT<KEYWORD, Keyword>
  fSharpExpression<EXPR, Expression>;

prefixAppExpr options { stubBase="FSharpExpressionBase"; }:
  fSharpExpression<FUNC_EXPR, FunctionExpression>
  fSharpExpression<ARG_EXPR, ArgumentExpression>;

binaryAppExpr options { stubBase="FSharpExpressionBase"; } extras {
  get { methodName="FunctionExpression" path=<binaryAppExpr:OP_REF_EXPR> };
}:
  fSharpExpression<LEFT_ARG_EXPR, LeftArgument>
  referenceExpr<OP_REF_EXPR, Operator>
  fSharpExpression<RIGHT_ARG_EXPR, RightArgument>;

letOrUseExpr options { stubBase="FSharpExpressionBase"; }:
  letOrUse{LET, BindingKeyword}
  REC<REC, RecKeyword>?
  LIST binding<BINDING, Bindings> SEP AND<FSHARP_AND, Separators>  
  IN<IN, InKeyword>?
  fSharpExpression<IN_EXPR, InExpression>;

interface tryLikeExpr:
  tryWithExpr |
  tryFinallyExpr;

tryWithExpr options { stubBase="FSharpExpressionBase"; }:
  TRY<TRY_KEYWORD, TryKeyword>
  fSharpExpression<TRY_EXPR, TryExpression>
  WITH
  matchClause<MATCH_CLAUSE, Clauses>*;

tryFinallyExpr options { stubBase="FSharpExpressionBase"; }:
  TRY<TRY_KEYWORD, TryKeyword>
  fSharpExpression<TRY_EXPR, TryExpression>
  FINALLY
  fSharpExpression<FINALLY_EXPR, FinallyExpression>;

lazyExpr options { stubBase="FSharpExpressionBase"; }:
  LAZY<KEYWORD, Keyword>
  fSharpExpression<EXPR, Expression>;

sequentialExpr options { stubBase="FSharpExpressionBase"; }:
  LIST fSharpExpression<EXPR, Expressions> SEP SEMICOLON<FSHARP_SEMI, Separators>;

ifThenElseExpr options { stubBase="FSharpExpressionBase"; }:
  IF<IF, IfKeyword>
  fSharpExpression<IF_EXPR, ConditionExpr>
  THEN<THEN, ThenKeyword>
  fSharpExpression<THEN_EXPR, ThenExpr>
  elseClause{ELSE_CLAUSE, ElseExpr}?;

elifExpr options { stubBase="FSharpExpressionBase"; }:
  ELIF<ELIF, ElifKeyword>
  fSharpExpression<IF_EXPR, ConditionExpr>
  THEN<THEN, ThenKeyword>
  fSharpExpression<THEN_EXPR, ThenExpr>
  elseClause{ELSE_CLAUSE, ElseExpr}?;

private elseClause{ROLE, Getter}:
  (
    ELSE<ELSE, ElseKeyword>
    fSharpExpression<@ROLE, @Getter>)
  |
  (
    elifExpr<@ROLE, @Getter>
  );

referenceExpr options { stubBase="FSharpExpressionBase"; } extras {
  get { methodName="TypeArguments" path=<referenceExpr:TYPE_ARG_LIST/prefixAppTypeArgumentList:TYPE> };
}:
  (
    fSharpExpression<QUALIFIER_EXPR, Qualifier>
    DOT<FSHARP_DELIMITER, Delimiter>
  )?
  fSharpIdentifier<IDENTIFIER, Identifier>
  prefixAppTypeArgumentList<TYPE_ARG_LIST, TypeArgumentList>?;

matchClause:
  BAR<BAR, Bar>?
  fSharpPattern<PAT, Pattern>
  whenExprClause<WHEN_EXPR, WhenExpression>?
  RARROW<RARROW, RArrow>
  fSharpExpression<EXPR, Expression>;
  
whenExprClause:
  WHEN<WHEN, WhenKeyword>
  fSharpExpression<EXPR, Expression>;

typeTestExpr options { stubBase="FSharpExpressionBase"; }:
  fSharpExpression<EXPR, Expression>
  COLON_QMARK
  typeUsage<TYPE, TypeUsage>;

upcastExpr options { stubBase="FSharpExpressionBase"; }:
  fSharpExpression<EXPR, Expression>
  COLON_GREATER<OP, OperatorToken>
  typeUsage<TYPE, TypeUsage>;
  
downcastExpr options { stubBase="FSharpExpressionBase"; }:
  fSharpExpression<EXPR, Expression>
  COLON_QMARK_GREATER<OP, OperatorToken>
  typeUsage<TYPE, TypeUsage>;

interface inferredCastExpr:
  inferredUpcastExpr |
  inferredDowncastExpr;

inferredUpcastExpr options { stubBase="FSharpExpressionBase"; }:
  UPCAST<KEYWORD, Keyword>
  fSharpExpression<EXPR, Expression>;

inferredDowncastExpr options { stubBase="FSharpExpressionBase"; }:
  DOWNCAST<KEYWORD, Keyword>
  fSharpExpression<EXPR, Expression>;

// todo: maybe replace with SynExpr.Const in FCS?
nullExpr options { stubBase="FSharpExpressionBase"; }:
  NULL<NULL, NullKeyword>;

addressOfExpr options { stubBase="FSharpExpressionBase"; }:
  (AMP | AMP_AMP)
  fSharpExpression<EXPR, Expression>;

traitCallExpr options { stubBase="FSharpExpressionBase"; }:
  fakeSeparator;

joinInExpr options { stubBase="FSharpExpressionBase"; }:
  fakeSeparator;

implicitZeroExpr options { stubBase="FSharpExpressionBase"; }:
  fakeSeparator;

yieldOrReturnExpr options { stubBase="FSharpExpressionBase"; }:
  (
    YIELD<YIELD_KEYWORD, YieldKeyword> |
    YIELD_BANG<YIELD_KEYWORD, YieldKeyword> |
    RETURN<YIELD_KEYWORD, YieldKeyword> |
    RETURN_BANG<YIELD_KEYWORD, YieldKeyword>
  )
  fSharpExpression<EXPR, Expression>;

libraryOnlyExpr options { stubBase="FSharpExpressionBase"; }:
  fakeSeparator;

fromErrorExpr options { stubBase="FSharpExpressionBase"; }:
  fSharpExpression<EXPR, Expression>;

interpolatedStringExpr options { stubBase="FSharpExpressionBase"; }:
  fakeSeparator;

fixedExpr options { stubBase="FSharpExpressionBase"; }:
  FIXED<KEYWORD, Keyword>
  fSharpExpression<EXPR, Expression>;

rangeSequenceExpr options { stubBase="FSharpExpressionBase"; }:
  fSharpExpression<FROM_EXPR, FromExpr>
  (
    DOT_DOT
    fSharpExpression<STEP_EXPR, StepExpr>
  )?
  DOT_DOT
  fSharpExpression<TO_EXPR, ToExpr>;

chameleonExpression:
  fSharpExpression<EXPR, Expression>;


typeDeclarationGroup extras {
  get { methodName="Attributes" path=<typeDeclarationGroup:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  TYPE<TYPE, TypeKeyword>
  LIST fSharpTypeOrExtensionDeclaration<TYPE_DECLARATION, TypeDeclarations> SEP AND<FSHARP_AND, Separators>;

interface fSharpTypeOrExtensionDeclaration:
  fSharpTypeDeclaration |
  typeExtensionDeclaration;

fSharpTypeDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="Attributes" path=<fSharpTypeDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
  get { methodName="TypeParameters" path=<fSharpTypeDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
//  get { methodName="TypeRepresentation" path=<fSharpTypeDeclaration:DECLARATION_BODY/typeDeclarationBody:TYPE_REPR> };
  get { methodName="TypeMembers" path=<fSharpTypeDeclaration:MEMBER_LIST/typeMemberDeclarationList:MEMBER_DECL> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  accessModifier{ACCESS_MODIFIER, AccessModifier}?
  fSharpIdentifier<IDENTIFIER, Identifier>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  primaryConstructorDeclaration<PRIMARY_CTOR_DECL, PrimaryConstructorDeclaration>?
  EQUALS<FSHARP_EQUALS, EqualsToken>
//  typeDeclarationBody<DECLARATION_BODY, DeclarationBody>;
  typeRepresentation<TYPE_REPR, TypeRepresentation>?
  typeMemberDeclarationList<MEMBER_LIST, MemberDeclarationsList>;


// typeDeclarationBody:
//   typeRepresentation<TYPE_REPR, TypeRepresentation>?
//   fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;

memberDeclarationList:
  memberDeclaration<MEMBER_DECL, TypeMembers>*;

typeMemberDeclarationList:
  fSharpTypeMemberDeclaration<MEMBER_DECL, TypeMembers>*;

interface typeRepresentation:
  simpleTypeRepresentation |
  objectModelTypeRepresentation |
  delegateRepresentation |
  typeAbbreviationRepresentation;

interface simpleTypeRepresentation options { customParseFunction; noInternalParseFunction; }:
  unionRepresentation |
  recordRepresentation |
  enumRepresentation;

interface objectModelTypeRepresentation:
  classRepresentation |
  structRepresentation |
  interfaceRepresentation;

classRepresentation options { stubBase="ObjectModelTypeRepresentationBase"; } extras {
  get { methodName="TypeMembers" path=<classRepresentation:MEMBER_LIST/typeMemberDeclarationList:MEMBER_DECL> };
}:
  CLASS<CLASS, ClassKeyword>
  typeMemberDeclarationList<MEMBER_LIST, MemberDeclarationsList>
  END;

structRepresentation options { stubBase="ObjectModelTypeRepresentationBase"; } extras {
  get { methodName="TypeMembers" path=<structRepresentation:MEMBER_LIST/typeMemberDeclarationList:MEMBER_DECL> };
}:
  STRUCT<STRUCT, StructKeyword>
  typeMemberDeclarationList<MEMBER_LIST, MemberDeclarationsList>
  END;

interfaceRepresentation options { stubBase="ObjectModelTypeRepresentationBase"; } extras {
  get { methodName="TypeMembers" path=<interfaceRepresentation:MEMBER_LIST/typeMemberDeclarationList:MEMBER_DECL> };
}:
  INTERFACE<INTERFACE, InterfaceKeyword>
  typeMemberDeclarationList<MEMBER_LIST, MemberDeclarationsList>
  END;

interface fSharpTypeOldDeclaration:
  exceptionDeclaration |
  moduleAbbreviationDeclaration |
  fSharpTypeOrExtensionDeclaration |
  unionCaseDeclaration;

nestedModuleDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="Attributes" path=<nestedModuleDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  MODULE<MODULE, ModuleOrNamespaceKeyword>
  REC<REC, RecKeyword>?
  accessModifier{ACCESS_MODIFIER, AccessModifier}?
  fSharpIdentifier<IDENTIFIER, Identifier>
  EQUALS<FSHARP_EQUALS, EqualsToken>
  moduleMember<MODULE_MEMBER, Members>*;

typeExtensionDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
   get { methodName="TypeParameters" path=<typeExtensionDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
   get { methodName="TypeMembers" path=<typeExtensionDeclaration:MEMBER_LIST/typeMemberDeclarationList:MEMBER_DECL> };
   get { methodName="Attributes" path=<typeExtensionDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
 }:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  accessModifier{ACCESS_MODIFIER, AccessModifier}?
  (
    referenceName<REFERENCE_NAME, QualifierReferenceName>
    DOT<FSHARP_DELIMITER, Delimiter>
  )?
  fSharpIdentifier<IDENTIFIER, Identifier>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  WITH<WITH, WithKeyword>
  typeMemberDeclarationList<MEMBER_LIST, MemberDeclarationsList>;


enumRepresentation options { stubBase="TypeRepresentationBase"; } extras {
  get { methodName="EnumMembers" path=<enumRepresentation:ENUM_CASE_LIST/enumCaseList:ENUM_MEMBER> };
}:
  accessModifier{ACCESS_MODIFIER, AccessModifier}?
  enumCaseList<ENUM_CASE_LIST, EnumCaseList>;

enumCaseList:
  enumMemberDeclaration<ENUM_MEMBER, EnumMembers>*;

enumMemberDeclaration options { stubBase="FSharpProperTypeMemberDeclarationBase"; } extras {
  get { methodName="Attributes" path=<enumMemberDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  IDENTIFIER<IDENTIFIER, Identifier>;


recordRepresentation options { stubBase="TypeRepresentationBase"; } extras {
  get { methodName="FieldDeclarations" path=<recordRepresentation:FIELD_DECLARATION_LIST/recordFieldDeclarationList:FIELD_DECLARATION> };
}:
  LBRACE
  accessModifier{ACCESS_MODIFIER, AccessModifier}?
  recordFieldDeclarationList<FIELD_DECLARATION_LIST, FieldDeclarationList>
  RBRACE;

recordFieldDeclarationList:
  recordFieldDeclaration<FIELD_DECLARATION, FieldDeclarations>*;

recordFieldDeclaration options { stubBase="FSharpProperTypeMemberDeclarationBase"; } extras {
  get { methodName="Attributes" path=<recordFieldDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  MUTABLE<MUTABLE, MutableKeyword>?
  IDENTIFIER<IDENTIFIER, Identifier>
  COLON<FSHARP_COLON, Colon>
  typeUsage<TYPE, Type>;


unionRepresentation options { stubBase="TypeRepresentationBase"; } extras {
  get { methodName="UnionCases" path=<unionRepresentation:UNION_CASE_LIST/unionCaseList:UNION_CASE> };
}:
  accessModifier{ACCESS_MODIFIER, AccessModifier}?
  unionCaseList<UNION_CASE_LIST, UnionCaseList>;

unionCaseList:
  unionCaseDeclaration<UNION_CASE, UnionCases>*;


interface unionCaseLikeDeclaration:
  unionCaseDeclaration |
  exceptionDeclaration |
  typeUsageOrUnionCaseDeclaration;

unionCaseDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<unionCaseDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
  get { methodName="Attributes" path=<unionCaseDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
  get { methodName="Fields" path=<unionCaseDeclaration:FIELD_DECLARATION_LIST/unionCaseFieldDeclarationList:FIELD_DECLARATION> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  accessModifier{ACCESS_MODIFIER, AccessModifier}?
  fSharpIdentifier<IDENTIFIER, Identifier>
  unionCaseFieldDeclarationList<FIELD_DECLARATION_LIST, FieldList>?
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;

unionCaseFieldDeclarationList:
  caseFieldDeclaration<FIELD_DECLARATION, Fields>*;

interface caseFieldDeclaration:
  unionCaseFieldDeclaration |
  exceptionFieldDeclaration;

unionCaseFieldDeclaration options { stubBase="UnionCaseFieldDeclarationBase"; }:
  IDENTIFIER<IDENTIFIER, Identifier>?
  typeUsage<TYPE, Type>;

exceptionFieldDeclaration options { stubBase="UnionCaseFieldDeclarationBase"; }:
  IDENTIFIER<IDENTIFIER, Identifier>?
  typeUsage<TYPE, Type>;


exceptionDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<exceptionDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
  get { methodName="Attributes" path=<exceptionDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
  get { methodName="Fields" path=<exceptionDeclaration:FIELD_DECLARATION_LIST/unionCaseFieldDeclarationList:FIELD_DECLARATION> };
  get { methodName="TypeMembers" path=<exceptionDeclaration:MEMBER_LIST/memberDeclarationList:MEMBER_DECL> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  accessModifier{ACCESS_MODIFIER, AccessModifier}?
  fSharpIdentifier<IDENTIFIER, Identifier>
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  unionCaseFieldDeclarationList<FIELD_DECLARATION_LIST, FieldList>?
  (
    WITH<WITH, WithKeyword>
    memberDeclarationList<MEMBER_LIST, MemberDeclarationsList>
  )?;


typeAbbreviationRepresentation options { stubBase="TypeRepresentationBase"; } extras {
  get { methodName="AbbreviatedType" path=<typeAbbreviationRepresentation:TYPE_OR_UNION_CASE/typeUsageOrUnionCaseDeclaration:TYPE> };
}:
  typeUsageOrUnionCaseDeclaration<TYPE_OR_UNION_CASE, AbbreviatedTypeOrUnionCase>;

typeUsageOrUnionCaseDeclaration options { stubBase="FSharpProperTypeMemberDeclarationBase"; } extras {
  get { methodName="Fields" path=<typeUsageOrUnionCaseDeclaration:FIELD_DECLARATION_LIST/unionCaseFieldDeclarationList:FIELD_DECLARATION> };
}:
  typeUsage<TYPE, TypeUsage>
  unionCaseFieldDeclarationList<FIELD_DECLARATION_LIST, FieldList>?; // for generating common interface

moduleAbbreviationDeclaration options { stubBase="FSharpTypeElementDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<moduleAbbreviationDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfTypeList:TYPE_PARAMETER> };
  get { methodName="Attributes" path=<moduleAbbreviationDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  MODULE<MODULE, ModuleOrNamespaceKeyword>
  accessModifier{ACCESS_MODIFIER, AccessModifier}?
  fSharpIdentifier<IDENTIFIER, Identifier>
  EQUALS<FSHARP_EQUALS, EqualsToken>
  typeReferenceName<TYPE_REFERENCE, TypeName>
  // todo: remove rules below
  typeParameterOfTypeList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  fSharpTypeMemberDeclaration<TYPE_MEMBER, TypeMembers>*;

interface activePatternCaseDeclaration:
  activePatternNamedCaseDeclaration |
  activePatternWildCase;

interface activePatternNamedCaseDeclaration:
  topActivePatternCaseDeclaration |
  localActivePatternCaseDeclaration;

topActivePatternCaseDeclaration options { stubBase="FSharpDeclarationBase"; }:
   IDENTIFIER<IDENTIFIER, Identifier>;

localActivePatternCaseDeclaration options { stubBase="LocalDeclarationBase"; }:
  IDENTIFIER<IDENTIFIER, Identifier>;

activePatternWildCase:
  wildPat;


delegateRepresentation options { stubBase="TypeRepresentationBase"; }:
  DELEGATE
  OF
  typeUsage<TYPE, Type>;

interface fSharpTypeMemberDeclaration:
  constructorDeclaration |
  inheritMember |
  interfaceImplementation |
  abstractMemberDeclaration |
  memberDeclaration |
  valFieldDeclaration |
  autoPropertyDeclaration |
  letBindingsDeclaration |
  doStatement |

  otherTypeMember;

interface constructorDeclaration:
  primaryConstructorDeclaration |
  memberConstructorDeclaration;

interface inheritMember:
  typeInherit |
  interfaceInherit;

interfaceInherit:
  INHERIT
  typeReferenceName<TYPE_REFERENCE, TypeName>;

typeInherit options { stubBase="ReferenceOwnerBase"; } extras {
  get { methodName="CtorArgExpression" path=<typeInherit:CHAMELEON_EXPR/chameleonExpression:EXPR> };
}:
  INHERIT
  typeReferenceName<TYPE_REFERENCE, TypeName>
  chameleonExpression<CHAMELEON_EXPR, CtorArg>;

interfaceImplementation extras {
  get { methodName="TypeMembers" path=<interfaceImplementation:MEMBER_LIST/memberDeclarationList:MEMBER_DECL> };
}:
  INTERFACE
  typeReferenceName<TYPE_REFERENCE, TypeName>
  (
    WITH<WITH, WithKeyword>
    memberDeclarationList<MEMBER_LIST, MemberDeclarationsList>
  )?;


otherTypeMember: fakeSeparator;

primaryConstructorDeclaration options { stubBase="FSharpProperTypeMemberDeclarationBase"; }:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  accessModifier{ACCESS_MODIFIER, AccessModifier}?
  parametersPatternDeclaration<PARAMETERS, ParametersPatternDeclaration>
  ctorSelfId<SELF_ID, SelfIdentifier>?;

interface selfId:
  ctorSelfId |
  memberSelfId;

ctorSelfId options { stubBase="LocalDeclarationBase"; }:
  AS
  IDENTIFIER<IDENTIFIER, Identifier>;

memberSelfId options { stubBase="LocalDeclarationBase"; }:
  IDENTIFIER<IDENTIFIER, Identifier>;  

memberConstructorDeclaration options { stubBase="FSharpProperTypeMemberDeclarationBase"; } extras {
  get { methodName="Expression" path=<memberConstructorDeclaration:CHAMELEON_EXPR/chameleonExpression:EXPR> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  accessModifier{ACCESS_MODIFIER, AccessModifier}?
  NEW<NEW, NewKeyword>
  parametersPatternDeclaration<PARAMETERS, ParametersPatternDeclaration>
  ctorSelfId<SELF_ID, SelfIdentifier>?
  EQUALS<FSHARP_EQUALS, EqualsToken>
  chameleonExpression<CHAMELEON_EXPR, ChameleonExpression>;



// todo: fix properties with explicit setters

memberDeclaration options { stubBase="FSharpProperTypeMemberDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<memberDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfMethodList:TYPE_PARAMETER> };
  get { methodName="Attributes" path=<memberDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
  get { methodName="Expression" path=<memberDeclaration:CHAMELEON_EXPR/chameleonExpression:EXPR> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  STATIC<STATIC, StaticKeyword>?
  memberKeyword{MEMBER_KEYWORD, MemberKeyword}
  accessModifier{ACCESS_MODIFIER, AccessModifier}?
  (
    memberSelfId<SELF_ID, SelfId>
    DOT<FSHARP_DELIMITER, Delimiter>
  )?
  identOrOpName{IDENTIFIER, Identifier}
  typeParameterOfMethodList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  (
    parametersPatternDeclaration<PARAMETERS_PATTERN, ParametersPatterns>*
    EQUALS<FSHARP_EQUALS, EqualsToken>
    chameleonExpression<CHAMELEON_EXPR, ChameleonExpression>
  |
    WITH
    accessorDeclaration<ACCESSOR_DECLARATION, AccessorDeclarations>
    (
      AND
      accessorDeclaration<ACCESSOR_DECLARATION, AccessorDeclarations>
    )?
  );

private memberKeyword{ROLE, Getter}:
  MEMBER<@ROLE, @Getter> |
  OVERRIDE<@ROLE, @Getter> |
  DEFAULT<@ROLE, @Getter>;

abstractMemberDeclaration options { stubBase="FSharpProperTypeMemberDeclarationBase"; } extras {
  get { methodName="TypeParameters" path=<abstractMemberDeclaration:TYPE_PARAMETERS_LIST/typeParameterOfMethodList:TYPE_PARAMETER> };
  get { methodName="Attributes" path=<abstractMemberDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  ABSTRACT<ABSTRACT, AbstractKeyword>
  MEMBER<MEMBER, MemberKeyword>?
  identOrOpName{IDENTIFIER, Identifier}
  typeParameterOfMethodList<TYPE_PARAMETERS_LIST, TypeParameterList>?
  parametersPatternDeclaration<PARAMETERS_PATTERN, ParametersPatterns>*;

valFieldDeclaration options { stubBase="FSharpProperTypeMemberDeclarationBase"; } extras {
  get { methodName="Attributes" path=<valFieldDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  STATIC<STATIC, StaticKeyword>?
  VAL<VAL, ValKeyword>
  IDENTIFIER<IDENTIFIER, Identifier>
  COLON<FSHARP_COLON, Colon>
  typeUsage<TYPE, Type>;

autoPropertyDeclaration options { stubBase="FSharpProperTypeMemberDeclarationBase"; }  extras {
  get { methodName="Expression" path=<autoPropertyDeclaration:CHAMELEON_EXPR/chameleonExpression:EXPR> };
  get { methodName="Attributes" path=<autoPropertyDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  STATIC<STATIC, StaticKeyword>?
  VAL<VAL, ValKeyword>
  MEMBER<MEMBER, MemberKeyword>
  IDENTIFIER<IDENTIFIER, Identifier>
  EQUALS<FSHARP_EQUALS, EqualsToken>
  chameleonExpression<CHAMELEON_EXPR, InitExpression>
  accessorsNamesClause<ACESSORS_CLAUSE, AccessorsClause>?;

accessorsNamesClause:
  WITH
  (
    IDENTIFIER<ACESSOR, AccessorsNames>
  |
    IDENTIFIER<ACESSOR, AccessorsNames>
    COMMA<FSHARP_COMMA, Comma>
    IDENTIFIER<ACESSOR, AccessorsNames>
  );

accessorDeclaration:
  IDENTIFIER<IDENTIFIER, Identifier>
  (
    parametersPatternDeclaration<PARAMETERS_PATTERN, ParametersPatterns>*
    EQUALS<FSHARP_EQUALS, EqualsToken>
    chameleonExpression<CHAMELEON_EXPR, BodyExpression>
  )?;

parametersPatternDeclaration:
  fSharpPattern<PATTERN, Pattern>;


// module statements

interface hashDirective:
  lineDirective |
  loadDirective |
  referenceDirective |
  iDirective |
  otherDirective;

lineDirective:
  PP_LINE<PP_LINE, HashToken>
  STRING<STRING, Args>*;

loadDirective:
  PP_LOAD<PP_LOAD, HashToken>
  STRING<STRING, Args>*;

referenceDirective:
  PP_REFERENCE<PP_REFERENCE, HashToken>
  STRING<STRING, Args>*;

iDirective:
  PP_I<PP_I, HashToken>
  STRING<STRING, Args>*;

otherDirective:
  PP_DIRECTIVE<PP_DIRECTIVE, HashToken>
  STRING<STRING, Args>*;

openStatement:
  OPEN<OPEN, Open>
  TYPE<TYPE, TypeKeyword>?
  typeReferenceName<TYPE_REFERENCE, ReferenceName>;

doStatement options { stubBase="DummyDeclaration"; } extras {
    get { methodName="Attributes" path=<doStatement:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
    get { methodName="Expression" path=<doStatement:CHAMELEON_EXPR/chameleonExpression:EXPR> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  DO<DO, DoKeyword>?
  chameleonExpression<CHAMELEON_EXPR, DoExpression>;



// Identifiers

private letOrUse{ROLE, Getter}:
  LET<@ROLE, @Getter> |
  USE<@ROLE, @Getter> |
  LET_BANG<@ROLE, @Getter> |
  USE_BANG<@ROLE, @Getter>;

private identOrOpName{ROLE, Getter}:
  IDENTIFIER<@ROLE, @Getter> |
  SYMBOLIC_OP<@ROLE, @Getter> |
  MINUS<@ROLE, @Getter> |
  PLUS<@ROLE, @Getter> |
  LPAREN_STAR_RPAREN<@ROLE, @Getter> |
  activePatternId<@ROLE, @Getter>;

activePatternId:
  LPAREN<LPAREN, LParen>
  BAR
  (
    activePatternCaseDeclaration<ACTIVE_PATTERN_CASE, Cases>
    BAR
  )+
  RPAREN<RPAREN, RParen>;

typeParameterId:
  QUOTE
  IDENTIFIER<IDENTIFIER, Identifier>;

interface fSharpIdentifier:
  identifier |
  symbolicOp |
  ampAmp |
  activePatternId |
  typeParameterId |
  global;

abstract identifier options { interfaceName="IFSharpIdentifier"; }:
  IDENTIFIER;

abstract symbolicOp options { interfaceName="IFSharpIdentifier"; }:
  SYMBOLIC_OP;

abstract ampAmp options { interfaceName="IFSharpIdentifier"; }:
  ampAmp;

abstract global options { interfaceName="IFSharpIdentifier"; }:
  GLOBAL;

// Type parameters

typeParameterOfTypeList:
  LESS<LESS,LAngle>
  typeParameterOfTypeDeclaration<TYPE_PARAMETER, TypeParameters>*
  GREATER<GREATER,RAngle>;

typeParameterOfMethodList:
  LESS<LESS,LAngle>
  typeParameterOfMethodDeclaration<TYPE_PARAMETER, TypeParameters>*
  GREATER<GREATER,RAngle>;

typeParameterOfTypeDeclaration options { stubBase="FSharpDeclarationBase"; } extras {
  get { methodName="Attributes" path=<typeParameterOfTypeDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  IDENTIFIER<IDENTIFIER, Identifier>;

typeParameterOfMethodDeclaration options { stubBase="FSharpDeclarationBase"; } extras {
  get { methodName="Attributes" path=<typeParameterOfMethodDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  IDENTIFIER<IDENTIFIER, Identifier>;


// Access modifiers

private accessModifier{ROLE,Getter}:
  PUBLIC<@ROLE,@Getter> |
  PRIVATE<@ROLE,@Getter> |
  INTERNAL<@ROLE,@Getter>;



// Attributes

attributeList:
  LBRACK_LESS<LBRACK, LBrack>
  LIST attribute<ATTRIBUTE, Attributes> SEP SEMICOLON<FSHARP_SEMI, Separators>
  GREATER_RBRACK<RBRACK, RBrack>;

attribute options { stubBase="TypeReferenceOwnerBase"; } extras {
  get { methodName="Expression" path=<attribute:CHAMELEON_EXPR/chameleonExpression:EXPR> };
}:
  attributeTarget<ATTR_TARGET, Target>?
  typeReferenceName<REFERENCE_NAME, ReferenceName>
  chameleonExpression<CHAMELEON_EXPR, ArgExpression>?;

attributeTarget:
  IDENTIFIER<IDENTIFIER, Identifier>
  COLON<FSHARP_COLON, Colon>;


fakeSeparator: FAKE;

// Local declarations

localDeclaration options { stubBase="LocalDeclarationBase"; } extras {
  get { methodName="Attributes" path=<localDeclaration:ATTRIBUTE_LIST/attributeList:ATTRIBUTE> };
}:
  attributeList<ATTRIBUTE_LIST, AttributeLists>*
  identOrOpName{IDENTIFIER, Identifier};


interface referenceName:
  expressionReferenceName |
  typeReferenceName;

interface typeArgumentList:
  prefixAppTypeArgumentList |
  postfixAppTypeArgumentList;


postfixAppTypeArgumentList:
  (
    typeUsage<TYPE, Types>
  |
    LPAREN
    LIST typeUsage<TYPE, Types> SEP COMMA<FSHARP_COMMA, Commas>
    RPAREN
 );

prefixAppTypeArgumentList:
  LESS<LESS,LAngle>
  LIST typeUsage<TYPE, Types> SEP COMMA<FSHARP_COMMA, Commas>
  GREATER<GREATER,RAngle>;


expressionReferenceName options { stubBase="ReferenceOwnerBase"; }:
  (
    expressionReferenceName<QUALIFIER, Qualifier>
    DOT<FSHARP_DELIMITER, Delimiter>
  )?
  fSharpIdentifier<IDENTIFIER, Identifier>
  prefixAppTypeArgumentList<TYPE_ARG, TypeArgumentList>;


typeReferenceName options { stubBase="TypeReferenceOwnerBase"; }:
  (
    postfixAppTypeArgumentList<TYPE_ARG, TypeArgumentList>
    (
      typeReferenceName<QUALIFIER, Qualifier>
      DOT<FSHARP_DELIMITER, Delimiter>
    )?
    fSharpIdentifier<IDENTIFIER, Identifier>
  |
    (
      typeReferenceName<QUALIFIER, Qualifier>
      DOT<FSHARP_DELIMITER, Delimiter>
    )?
    fSharpIdentifier<IDENTIFIER, Identifier>
    prefixAppTypeArgumentList<TYPE_ARG, TypeArgumentList>
  );


interface typeUsage:
  namedTypeUsage |
  tupleTypeUsage |
  anonRecordTypeUsage |
  arrayTypeUsage |
  functionTypeUsage |
  anonTypeUsage |
  parenTypeUsage |
  unsupportedTypeUsage;


namedTypeUsage:
  typeReferenceName<TYPE_REFERENCE, ReferenceName>;

tupleTypeUsage:
  typeUsage<ITEM, Items>
  (
    STAR
    typeUsage<ITEM, Items>
  )*;

anonRecordTypeUsage:
  LBRACE_BAR
  anonRecordField<FIELD, Fields>*
  BAR_RBRACE;

anonRecordField:
  expressionReferenceName<REFERENCE_NAME, ReferenceName>
  COLON<FSHARP_COLON, Colon>
  typeUsage<TYPE, Type>;

arrayTypeUsage:
  typeUsage<TYPE, Type>
  LBRACK
  RBRACK;

functionTypeUsage:
  typeUsage
  RARROW
  typeUsage;

anonTypeUsage:
  UNDERSCORE;

parenTypeUsage:
  LPAREN<LPAREN, LeftParen>
  typeUsage<TYPE, InnerTypeUsage>
  RPAREN<RPAREN, RightParen>;

unsupportedTypeUsage:
  fakeSeparator;
